# Generate SWIG wrapper (for both MATLAB)

set(Matlab_FIND_COMPONENTS "MAIN_PROGRAM")
FIND_PACKAGE(MATLAB)

IF (NOT DISABLE_SWIG AND SWIG_EXECUTABLE)

execute_process(COMMAND ${SWIG_EXECUTABLE} -help OUTPUT_VARIABLE SWIG_HELP_OUTPUT)
STRING(FIND "${SWIG_HELP_OUTPUT}" "-matlab" MATLAB_HELP_FOUND)
if (${MATLAB_HELP_FOUND} LESS 0)
set (MATLAB_SWIG_NOT_AVAILABLE 1)
message(warning " SWIG VERSION does not support matlab, reverting to build only")
endif()
ENDIF()
MESSAGE(STATUS "Building MATLAB")
IF (DISABLE_SWIG OR MATLAB_SWIG_NOT_AVAILABLE)

ADD_CUSTOM_COMMAND(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp
  COMMAND "${CMAKE_COMMAND}"
    -E copy ${CMAKE_CURRENT_SOURCE_DIR}/helicsMEX.cpp ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp
)
INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/+helics DESTINATION matlab)

FILE(GLOB MATLAB_FILES ${CMAKE_CURRENT_SOURCE_DIR}/*.m)
INSTALL(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/+helics DESTINATION matlab)
INSTALL(FILES ${MATLAB_FILES} DESTINATION matlab)
ELSE()

FILE(GLOB SHARED_LIB_HEADERS ${CMAKE_SOURCE_DIR}/src/helics/shared_api_library/*.h)

#custom command for building the wrap file
ADD_CUSTOM_COMMAND(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp
  COMMAND "${SWIG_EXECUTABLE}"
    "-matlab" "-c++"
    -o "helicsMEX.cpp"
	"-I${CMAKE_SOURCE_DIR}/src/helics/shared_api_library"
	${CMAKE_CURRENT_SOURCE_DIR}/helicsMatlab.i
  DEPENDS ${CMAKE_SOURCE_DIR}/swig/helics.i ${CMAKE_CURRENT_SOURCE_DIR}/helicsMatlab.i ${SHARED_LIB_HEADERS}
)

#ADD_CUSTOM_COMMAND(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp
#  COMMAND ${CMAKE_COMMAND} -P ${CMAKE_CURRENT_SOURCE_DIR}/modifyOctSourceFile.cmake
#  DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp
#)

INSTALL(DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/+helics DESTINATION matlab)
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/SwigGet.m ${CMAKE_CURRENT_BINARY_DIR}/SwigMem.m ${CMAKE_CURRENT_BINARY_DIR}/SwigRef.m DESTINATION matlab)
INSTALL(FILES ${CMAKE_CURRENT_SOURCE_DIR}/pisender.m ${CMAKE_CURRENT_SOURCE_DIR}/pireceiver.m ${CMAKE_CURRENT_SOURCE_DIR}/GetFullPath.m DESTINATION matlab)
ENDIF()

ADD_CUSTOM_COMMAND(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/mkhelicsMEXFile.m
  COMMAND ${CMAKE_COMMAND} 
  -D LIBRARY_FILE=$<TARGET_LINKER_FILE:helicsSharedLib> 
  -D BUILD_FILE=${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp 
  -D LIBRARY_INCLUDE_LOCATION=${CMAKE_SOURCE_DIR}/src/helics/shared_api_library/
  -D SOURCE_DIR=${CMAKE_CURRENT_SOURCE_DIR}
  -P ${CMAKE_CURRENT_SOURCE_DIR}/generateMEXcreationScript.cmake
  DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.cpp
)

ADD_CUSTOM_COMMAND(OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.${Matlab_MEX_EXTENSION}
  COMMAND ${Matlab_MAIN_PROGRAM} -nojvm -nodesktop -r "\"run('${CMAKE_CURRENT_BINARY_DIR}/mkhelicsMEXFile.m');quit;\""
  DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/mkhelicsMEXFile.m helicsSharedLib
)

ADD_CUSTOM_TARGET(helicsMEX ALL
   DEPENDS ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.${Matlab_MEX_EXTENSION}
)

set_target_properties (helicsMEX PROPERTIES FOLDER interfaces)

INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/helicsMEX.${Matlab_MEX_EXTENSION} DESTINATION matlab)
INSTALL(FILES $<TARGET_FILE:helicsSharedLib> DESTINATION matlab)
INSTALL(FILES ${KEY_LIBRARY_FILES} DESTINATION matlab)
