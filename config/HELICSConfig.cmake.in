# HELICS cmake module
# This module sets the following variables in your project::
#
#   HELICS_FOUND - true if HELICS found on the system
#   HELICS_INCLUDE_DIR - the directory containing HELICS headers
#   HELICS_C_SHARED_LIBRARY - the C shared library
# 	HELICS_CXX_SHARED_LIBRARY -the C++ shared library
#   HELICS_STATIC_LIBRARY -the static Library
# 	HELICS_DEPENDENCIES -- list of dependencies in the helics project
#   HELICS_PLAYER -- the player executable
#  	HELICS_RECORDER -- the recorder executable
#   HELICS_APP  -- the helics app executable
#   HELICS_BROKER -- the helics broker executable
@PACKAGE_INIT@

set(PN HELICS)
set(${PN}_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@")

set(C_SHARED_BUILT @BUILT_C_SHARED_LIB@)
if (C_SHARED_BUILT )
set(${PN}_C_SHARED_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_c_shared_file@")
set(${PN}_C_SHARED_LIBRARY_DEBUG "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_c_shared_file_debug@")
endif()

set(CXX_SHARED_BUILT @BUILD_CXX_SHARED_LIB@)
if (CXX_SHARED_BUILT)
set(${PN}_CXX_SHARED_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_cxx_shared_file@")
set(${PN}_CXX_SHARED_LIBRARY_DEBUG "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_cxx_shared_file_debug@")
endif()

set(${PN}_STATIC_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/@helics_static_file@")
set(${PN}_STATIC_LIBRARY_DEBUG "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/@helics_static_file_debug@")

find_program(HELICS_PLAYER helics_player 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
find_program(HELICS_RECORDER helics_recorder 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
find_program(HELICS_APP helics_app 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
find_program(HELICS_BROKER helics_broker 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
set(ENABLE_ZMQ_CORE @ENABLE_ZMQ_CORE@)
set(ENABLE_MPI_CORE @ENABLE_MPI_CORE@)

set(SAVED_PARENT_CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH})
set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

include(CMakeFindDependencyMacro)

if (ENABLE_ZMQ_CORE)
	if (NOT TARGET libzmq)
		set(ZeroMQ_LIBRARY_ONLY ON)
		if(NOT ZeroMQ_INSTALL_PATH)
			if(ENV{ZeroMQ_INSTALL_PATH})
				set(ZeroMQ_INSTALL_PATH $ENV{ZeroMQ_INSTALL_PATH})
			else()
                # Check if a copy of ZeroMQ was installed with HELICS
				set(ZeroMQ_LIBRARY_PATH "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/")
                find_package(ZeroMQ)
			endif()
		endif()
        # If ZeroMQ wasn't installed with this copy of HELICS, search for a system copy 
        if (NOT ZeroMQ_FOUND AND NOT TARGET libzmq)
            unset(ZeroMQ_LIBRARY_PATH)
            find_dependency(ZeroMQ)
        endif()
	endif()
endif()

if (ENABLE_MPI_CORE)
	if (NOT TARGET MPI::MPI_C)
		include(addMPI)
	endif()
endif()

if (NOT TARGET Threads::Threads)
	if (NOT WIN32)
		set(THREADS_PREFER_PTHREAD_FLAG ON)
	elseif (MSYS)
		set(THREADS_PREFER_PTHREAD_FLAG ON)
	endif()
	find_dependency(Threads REQUIRED)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/helics-targets.cmake)

check_required_components(${PN})

set(CMAKE_MODULE_PATH ${SAVED_PARENT_CMAKE_MODULE_PATH})
