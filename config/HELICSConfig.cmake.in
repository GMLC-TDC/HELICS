# HELICS cmake module
# This module sets the following variables in your project::
#
#   HELICS_FOUND - true if HELICS found on the system
#   HELICS_INCLUDE_DIR - the directory containing HELICS headers
#   HELICS_C_SHARED_LIBRARY - the C shared library
# 	HELICS_CXX_SHARED_LIBRARY -the C++ shared library
#   HELICS_STATIC_LIBRARY -the static Library
# 	HELICS_DEPENDENCIES -- list of dependencies in the helics project
#   HELICS_PLAYER -- the player executable
#  	HELICS_RECORDER -- the recorder executable
#   HELICS_APP  -- the helics app executable
#   HELICS_BROKER -- the helics broker executable
@PACKAGE_INIT@

set(PN HELICS)
set_and_check(${PN}_INCLUDE_DIR "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_INCLUDEDIR@")

set(C_SHARED_BUILT @BUILT_C_SHARED_LIB@)
if (C_SHARED_BUILT )
set(${PN}_C_SHARED_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_c_shared_file@")
set(${PN}_C_SHARED_LIBRARY_DEBUG "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_c_shared_file_debug@")
endif()

set(CXX_SHARED_BUILT @BUILD_CXX_SHARED_LIB@)
if (CXX_SHARED_BUILT)
set(${PN}_CXX_SHARED_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_cxx_shared_file@")
set(${PN}_CXX_SHARED_LIBRARY_DEBUG "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@/@helics_cxx_shared_file_debug@")
endif()

set(${PN}_STATIC_LIBRARY "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/@helics_static_file@")
set(${PN}_STATIC_LIBRARY_DEBUG "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/@helics_static_file_debug@")

find_program(HELICS_PLAYER helics_player 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
find_program(HELICS_RECORDER helics_recorder 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
find_program(HELICS_APP helics_app 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
find_program(HELICS_BROKER helics_broker 
	PATH ${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_BINDIR@
	NO_DEFAULT_PATH)
	
set(HELICS_HAVE_ZEROMQ @HELICS_HAVE_ZEROMQ@)
set(HELICS_HAVE_MPI @HELICS_HAVE_MPI@)

set(CMAKE_MODULE_PATH ${PACKAGE_PREFIX_DIR}/@HELICS_CMAKECONFIG_INSTALL_DIR@)

include(CMakeFindDependencyMacro)
include(extraMacros)

if (TARGET Boostlibs::core)
else()
	include(addBoost)
endif()

if (HELICS_HAVE_ZEROMQ)
	if (TARGET libzmq)
	else()
		set(ZeroMQ_LIBRARY_ONLY ON)
		set(ZeroMQ_INSTALL_PATH ${PACKAGE_PREFIX_DIR})
		find_dependency(ZeroMQ)
	endif()
endif()

if (HELICS_HAVE_MPI)
	if (TARGET MPI::MPI_C)
	else()
		include(addMPI)
	endif()
endif()

#find_dependency(ZeroMQ)

if (TARGET Threads::Threads)
else()
	if (NOT WIN32)
		set(THREADS_PREFER_PTHREAD_FLAG ON)
	elseif (MSYS)
		set(THREADS_PREFER_PTHREAD_FLAG ON)
	endif()
	find_dependency(Threads REQUIRED)
endif()

include(${CMAKE_CURRENT_LIST_DIR}/helics-targets.cmake)

if (TARGET HELICS::helicsSharedLib)
	add_library(HELICS::helicsCpp98 INTERFACE IMPORTED)
	set_property(TARGET HELICS::helicsCpp98 PROPERTY INTERFACE_INCLUDE_DIRECTORIES "${PACKAGE_PREFIX_DIR}/include/cpp98")

	set_property(TARGET HELICS::helicsCpp98 PROPERTY INTERFACE_LINK_LIBRARIES HELICS::helicsSharedLib)

	set_target_properties(HELICS::helicsSharedLib PROPERTIES
		INTERFACE_INCLUDE_DIRECTORIES "${PACKAGE_PREFIX_DIR}/include/helics/shared_api_library"
	)
endif()

check_required_components(${PN})

